---
layout: default

previousLabel: Artists
previousHref: build-it/artists.html
nextLabel: Albums &amp; Artists
nextHref: build-it/albums-artists.html
---
<div class="jumbotron">
	<h1>Playlistr: Albums</h1>

	<p class="lead">Let's add an <code>Album</code> feature to our application and introduce some new ideas.</p>
</div>

<div class="row">
	<div class="col">
		<h2>Albums</h2>
	</div>

	<div class="w-100"></div>

	<div class="col">
		<p>Let's start with another simple <code>CRUD</code> feature. This time to create, read, update and delete an <code>Album</code> in our application. We're going to introduce a few new ideas with this one.</p>

		<p>This time we'll use <code>qb</code> inside our <code>commandbox-migrations</code> script rather than raw <code>SQL</code>.</p>

		<h3>Migrate Create</h3>

		<p>If you remember, we created our <code>commandbox-migrations</code> script in the terminal. We can do it again with <code>migrate create albums</code></p>

		<p>With the script template we can add our <code>qb</code> functions to build our table schema.</p>

		{%- capture content %}
			{%- highlight cfscript %}
component {

	function up( schema ) {
		schema.create( "albums", function( table ) {
			table.increments( "id" );

			table.string( "name" );
		} );
	}

	function down( schema ) {
		schema.drop( "albums" );
	}

}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="resources/database/migrations/2018_07_02_100000_albums.cfc"
		-%}

		<p>With <code>qb</code> we have access to closure methods that can create and drop a table as well as add columns definitions. We pass the <code>schema</code> from <code>qb</code> into our <code>up()</code> and <code>down()</code> methods to give us access to the features of <code>qb</code>.</p>

		<p>The <code>create()</code> closure takes the name of the table we want to create, "<em>albums</em>", and a function to define the columns in that table. The <code>increments()</code> method adds an auto-incrementing integer primary key and <code>string()</code> adds a <code>varchar(255)</code> string column to the table.</p>

		<p>In the <code>down()</code> method we'll use <code>drop()</code> to delete our "<em>albums</em>" table.</p>

		<h3>Album.cfc (Model)</h3>

		<p>This is the same as the <code>Artist</code> entity and saved as <code>models/Album.cfc</code></p>

		<h3>index.cfm &amp; createUpdate.cfm (Views)</h3>

		<p>These are also the same as those for the <code>Artist</code>.</p>

		<p>Next we create our <code>Album</code> service and handler.</p>

		<h3>Service &amp; Handler</h3>

		<hr>
	</div>
</div>

<div class="row">
	<div class="col-6">
		<h3>Album Service</h3>

		<p>Now let's introduce the idea of interacting with our database via a Service component. Add the file <code>models/AlbumService.cfc</code> to your webroot. The start of this Service component should start by injecting <code>WireBox</code> and include a simple <code>init()</code> constructor method.</p>

		{%- capture content %}
			{%- highlight cfscript %}
component {
	property wirebox inject="wirebox";

	public AlbumService function init() {
		return this;
	}
}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="models/AlbumService.cfc"
		-%}

		<p>Now we can add new methods to build out the functionality to interface with our database using <code>Quick</code> features.</p>

		<h4>getByID()</h4>

		{%- capture content %}
			{%- highlight cfscript %}
public Album function getByID( required any id) {
	return wirebox.getInstance( "Album" )
		.findOrFail( id );
}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="models/AlbumService.cfc"
		-%}

		<p>This should look similar to a portion of the <code>update()</code> action from the <code>Artists</code> handler.</p>

		<h4>getAllAsArray()</h4>

		{%- capture content %}
			{%- highlight cfscript %}
public array function getAllAsArray() {
	return wirebox.getInstance( "Album" )
		.orderby( "id" )
		.get()
		.toArray();
}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="models/AlbumService.cfc"
		-%}

		<p>Once again this should look familiar to a portion of the <code>index()</code> action. This time we've chained the <code>toArray()</code> method to our <code>get()</code> to convert the collection to an array before sending it back.</p>

		<h4>create()</h4>

		{%- capture content %}
			{%- highlight cfscript %}
public Album function create( required struct values ) {
	return wirebox.getInstance( "Album" )
		.create( values );
}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="models/AlbumService.cfc"
		-%}

		<p>Another similar method. This time we take in a structure of the name/value pairs and pass that on to populate a new row in our database.</p>

		<h4>update()</h4>

		{%- capture content %}
			{%- highlight cfscript %}
public void function update( required any id, required struct values ) {
	getByID( id )
		.update( values );
}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="models/AlbumService.cfc"
		-%}

		<p>Here we chain the <code>update()</code> onto the end of the <code>getByID()</code> method we wrote earlier and pass on the structure of names/values to update an existing row in our database.</p>

		<h4>delete()</h4>

		{%- capture content %}
			{%- highlight cfscript %}
public void function delete( required any id ) {
	getByID( id )
		.delete();
}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="models/AlbumService.cfc"
		-%}

		<p>Similar to <code>update()</code>, we chain to the end of <code>getByID()</code> to delete an existing row from our database.</p>
	</div>

	<div class="col-6">
		<h3>Album Handler</h3>

		<p>With our new <code>AlbumService</code> we can simplify our handler. So first create your handler <code>handlers/Albums.cfc</code> and start with:</p>

		{%- capture content %}
			{%- highlight cfscript %}
component extends="coldbox.system.EventHandler" {
	property AlbumService inject="AlbumService";
}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="handlers/Albums.cfc"
		-%}

		<p>We've added <code>property AlbumService inject="AlbumService";</code> to the component pseudo constructor to inject the <code>AlbumService</code> into the handler using the <code>WireBox</code> convention.</p>

		<h4>Action: index()</h4>

		{%- capture content %}
			{%- highlight cfscript %}
function index( event, rc, prc ) {
	prc.albums = AlbumService.getAllAsArray();

	if( prc.albums.len() == 0 ) {
		relocate( "albums.create" );
	}
}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="handlers/Albums.cfc"
		-%}

		<p>We capture all the <code>Albums</code> into the variable <code>prc.albums</code> for our view with the <code>getAllAsArray()</code> method in the <code>AlbumService</code> component but bounce the visitor onto the create action if none are returned.</p>

		<h4>Action: create()</h4>

		<p>This is the same as <code>Artists</code>.</p>

		<h4>Action: createAction()</h4>

		{%- capture content %}
			{%- highlight cfscript %}
function createAction( event, rc, prc ) {
	param rc.name = "";

	AlbumService.create( {
		name: rc.name
	} );

	relocate( "albums" );
}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="handlers/Albums.cfc"
		-%}

		<p>This is similar to the <code>Artists</code> action but it now uses the <code>AlbumService</code> to create the database row.</p>

		<h4>Action: update()</h4>

		{%- capture content %}
			{%- highlight cfscript %}
function update( event, rc, prc ) {
	param rc.id = "";

	var album = AlbumService.getByID( rc.id );

	prc.name = album.getName();

	prc.action = "Update";
	prc.formAction = "albums.updateAction";

	event.setView( "albums/createUpdate" );
}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="handlers/Albums.cfc"
		-%}

		<p>Once again this is similar to the <code>Artists</code> action but it now uses the <code>AlbumService</code> to get our <code>Album</code> for updating.</p>

		<h4>Action: updateAction()</h4>

		{%- capture content %}
			{%- highlight cfscript %}
function updateAction( event, rc, prc ) {
	param rc.id = "";
	param rc.name = "";

	AlbumService.update( rc.id, {
		name: rc.name
	} );

	relocate( "albums" );
}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="handlers/Albums.cfc"
		-%}

		<p>Again, like the <code>Artists</code> action we use the <code>AlbumService</code> to update our <code>Album</code> by passing it the <code>id</code> and name/value structure of the columns to be updated.</p>

		<h4>Action: delete()</h4>

		{%- capture content %}
			{%- highlight cfscript %}
function delete( event, rc, prc ) {
	param rc.id = "";

	AlbumService.delete( rc.id );

	relocate( "albums" );
}
			{%- endhighlight %}
		{%- endcapture %}

		{%- include figure.html
			content=content
			caption="handlers/Albums.cfc"
		-%}

		<p>Finally the <code>delete()</code> action uses the <code>AlbumService</code> to removed the selected row from the database.</p>
	</div>
</div>

{% include branch.html branch='0.1.1-albums-crud' %}

{% include footerNav.html
	previousLabel=page.previousLabel
	previousHref=page.previousHref
	nextLabel=page.nextLabel
	nextHref=page.nextHref
%}
